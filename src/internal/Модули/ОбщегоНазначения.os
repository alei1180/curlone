#Область ПрограммныйИнтерфейс

Процедура ДополнитьМассив(МассивПриемник, МассивИсточник) Экспорт
	Для Каждого Значение Из МассивИсточник Цикл
		МассивПриемник.Добавить(Значение);
	КонецЦикла;
КонецПроцедуры

Функция ДобавитьКонечныйРазделительПути(Знач ПутьКаталога) Экспорт

	Если ПустаяСтрока(ПутьКаталога) Тогда
		Возврат ПутьКаталога;
	КонецЕсли;

	ПоследнийСимвол = Прав(ПутьКаталога, 1);

	Если ПоследнийСимвол = "/" Или ПоследнийСимвол = "\" Тогда
		Возврат ПутьКаталога;
	КонецЕсли;

	Разделитель = ?(СтрНайти(ПутьКаталога, "\"), "\", "/");
	
	Возврат ПутьКаталога + Разделитель;

КонецФункции

Функция КаталогФайла(ПутьКФайлу) Экспорт

	ИндексРазделителя1 = СтрНайти(ПутьКФайлу, "\", НаправлениеПоиска.СКонца);
	ИндексРазделителя2 = СтрНайти(ПутьКФайлу, "/", НаправлениеПоиска.СКонца);

	ИндексРазделителя = Макс(ИндексРазделителя1, ИндексРазделителя2);
	
	Если ИндексРазделителя Тогда
		Возврат Сред(ПутьКФайлу, 1, ИндексРазделителя - 1);
	КонецЕсли;

	Возврат "";

КонецФункции

Функция ДополнитьИменемФайлаПутьURL(ПолноеИмяФайла, ПутьURL) Экспорт

	Файл = Новый Файл(ПолноеИмяФайла);

	Если ПустаяСтрока(ПутьURL) Тогда
		Возврат "/" + Файл.Имя;
	ИначеЕсли Прав(ПутьURL, 1) = "/" Тогда
		Возврат ПутьURL + Файл.Имя;
	Иначе
		Возврат ПутьURL;
	КонецЕсли;

КонецФункции

Функция ПротоколПроксиПоддерживатся(Прокси) Экспорт

	ПоддерживаемыеПротоколыПрокси = Новый Массив();
	ПоддерживаемыеПротоколыПрокси.Добавить(ПротоколыURL.HTTP);
	ПоддерживаемыеПротоколыПрокси.Добавить(ПротоколыURL.HTTPS);
	ПоддерживаемыеПротоколыПрокси.Добавить(ПротоколыURL.FTP);
	ПоддерживаемыеПротоколыПрокси.Добавить(ПротоколыURL.FTPS);

	Возврат ПоддерживаемыеПротоколыПрокси.Найти(Прокси) <> Неопределено;

КонецФункции

Функция ЕстьКритичныеОшибки(Ошибки) Экспорт
	Для Каждого Ошибка Из Ошибки Цикл
		Если Ошибка.Критичная Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;
КонецФункции

Функция НоваяКритичнаяОшибка(Текст) Экспорт
	Возврат НоваяОшибка(Текст, Истина);
КонецФункции

Функция НоваяОшибка(Текст, Критичная = Ложь) Экспорт
	Возврат Новый Структура("Текст, Критичная", Текст, Критичная);
КонецФункции

Функция ЭтоHTTP(Схема) Экспорт
	Возврат Схема = ПротоколыURL.HTTP Или Схема = ПротоколыURL.HTTPS;
КонецФункции

Функция ЭтоFTP(Схема) Экспорт
	Возврат Схема = ПротоколыURL.FTP Или Схема = ПротоколыURL.FTPS;
КонецФункции

#КонецОбласти