Перем _ОписаниеЗапроса;  // ОписаниеЗапроса
Перем _Файлы;            // Массив из ПередаваемыйФайл
Перем _ПрочитанныеФайлы; // Массив из Структура:
                         //   * ПередаваемыйФайл - ПередаваемыйФайл
                         //   * ИмяПеременной - Строка

// Конструктор объекта.
//
// Параметры:
//   ОписаниеЗапроса  - ОписаниеЗапроса
//   Файлы            - Массив из ПередаваемыйФайл - Файлы для передачи
//   ПрочитанныеФайлы - Массив из Структура - Массив для хранения информации о прочитанных файлах: 
//                        * ПередаваемыйФайл - ПередаваемыйФайл
//                        * ИмяПеременной - Строка
Процедура ПриСозданииОбъекта(ОписаниеЗапроса, Файлы, ПрочитанныеФайлы)
	_ОписаниеЗапроса = ОписаниеЗапроса;
	_Файлы = Файлы;
	_ПрочитанныеФайлы = ПрочитанныеФайлы;
КонецПроцедуры

// Формирует программный код чтения содержимого файлов.
//
// Возвращаемое значение:
//   Строка
Функция Получить() Экспорт

	Конструктор = Новый КонструкторПрограммногоКода(_ОписаниеЗапроса.ИменаПеременных);

	НомерФайла = 1;
	Для Каждого ПередаваемыйФайл Из _Файлы Цикл

		Если Не (ПередаваемыйФайл.ПрочитатьСодержимое 
			Или ПередаваемыйФайл.Назначение = НазначенияПередаваемыхДанных.СтрокаЗапроса) Тогда
			Продолжить;
		КонецЕсли;

		Если НомерФайла > 1 Тогда
			Конструктор.ДобавитьПустуюСтроку();
		КонецЕсли;

		Шаблон = "ЧтениеТекста = Новый ЧтениеТекста(%2);
			|%1 = ЧтениеТекста.Прочитать();";

		Если ПередаваемыйФайл.УдалятьПереносыСтрок Тогда
			Шаблон = Шаблон + "
			|%1 = СтрЗаменить(%1, Символы.ПС, """");
			|%1 = СтрЗаменить(%1, Символы.ВК, """");";
		КонецЕсли;

		Если ПередаваемыйФайл.КодироватьСодержимое Тогда
			Шаблон = Шаблон + "
			|%1 = КодироватьСтроку(%1, СпособКодированияСтроки.КодировкаURL);";
		КонецЕсли;

		ИмяПеременной = "{t(Переменная.ТекстовыеДанныеФайла)}_" + Формат(НомерФайла, "ЧГ=");

		Конструктор.ДобавитьСтроку(Шаблон,
			ИмяПеременной,
			Конструктор.ПараметрВСтроку(ПередаваемыйФайл.ПолноеИмяФайла)
		);

		ПрочитанныйФайл = Новый Структура();
		ПрочитанныйФайл.Вставить("ПередаваемыйФайл", ПередаваемыйФайл);
		ПрочитанныйФайл.Вставить("ИмяПеременной", ИмяПеременной);

		_ПрочитанныеФайлы.Добавить(ПрочитанныйФайл);

		НомерФайла = НомерФайла + 1;

	КонецЦикла;

	Возврат Конструктор.ПолучитьРезультат();

КонецФункции
