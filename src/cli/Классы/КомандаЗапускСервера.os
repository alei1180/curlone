&Пластилин Перем Настройки; // Настройки
&Пластилин Перем ЗапускательВебПриложения; // ЗапускательВебПриложения

&Опция(Имя = "p port", Описание = "Порт для запуска веб-сервера")
&ТЧисло
Перем Порт; // Число

&Опция(Имя = "o open", Описание = "Открыть в браузере")
&ТБулево
&ПоУмолчанию(Ложь)
Перем ОткрытьВБраузере; // Булево

&Деталька("winow.Порт") 
Перем ПортПоУмолчанию; // Число

&КомандаПриложения(Имя = "web", Описание = "Запуск веб-сервера")
Процедура ПриСозданииОбъекта()

КонецПроцедуры

&ВыполнениеКоманды
Процедура Запустить() Экспорт

	Настройки.Порт = Порт();
	
	Если ОткрытьВБраузере Тогда
		ФоновыеЗадания.Выполнить(ЭтотОбъект, "ЗапуститьБраузерСЗадержкой");
	КонецЕсли;

	ЗапускательВебПриложения.Запустить();

КонецПроцедуры

Процедура ЗапуститьБраузерСЗадержкой() Экспорт

	ЗадержкаПередЗапуском = 500; // мс
	Адрес = СтрШаблон("http://%1:%2", Настройки.ИмяХоста, Формат(Порт(), "ЧГ=0"));
	
	Приостановить(ЗадержкаПередЗапуском);
	ЗапуститьПриложение(Адрес);
	
КонецПроцедуры

Функция Порт()
	Возврат ?(ЗначениеЗаполнено(Порт), Порт, ПортПоУмолчанию);
КонецФункции