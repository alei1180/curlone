#Использовать asserts

Перем _КонвертерКомандыCURL;
Перем _ГенераторПоУмолчанию;

Процедура ПриСозданииОбъекта(КонвертерКомандыCURL, ГенераторПоУмолчанию)
	_КонвертерКомандыCURL = КонвертерКомандыCURL;
	_ГенераторПоУмолчанию = ГенераторПоУмолчанию;
КонецПроцедуры

Процедура Проверить(КонсольнаяКоманда, ПрограммныйКод, Генератор = Неопределено) Экспорт

	Если Генератор = Неопределено Тогда
		Генератор = _ГенераторПоУмолчанию;
	КонецЕсли;

	// Результат конвертации
	Ошибки = Неопределено;

	_КонвертерКомандыCURL.УстановитьЯзыкПеревода("ru");
	Результат = _КонвертерКомандыCURL.Конвертировать(КонсольнаяКоманда, Генератор, Ошибки);

	Ожидаем.Что(Результат, КонсольнаяКоманда).Равно(ПрограммныйКод);
	Ожидаем.Что(Ошибки, КонсольнаяКоманда).Не_().Заполнено();

	// Перевод
	Если СодержитРусскиеБуквы(КонсольнаяКоманда) Тогда
		Возврат;
	КонецЕсли;

	_КонвертерКомандыCURL.УстановитьЯзыкПеревода("en");
	РезультатПеревода = _КонвертерКомандыCURL.Конвертировать(КонсольнаяКоманда, Генератор, Ошибки);
	Сообщение = СтрШаблон("Перевод %1:
	|%2", КонсольнаяКоманда, РезультатПеревода);

	Ожидаем.Что(РезультатПеревода).Не_().Содержит("{t(");
	Ожидаем.Что(СодержитРусскиеБуквы(РезультатПеревода), Сообщение).ЭтоЛожь();

КонецПроцедуры

Функция СодержитРусскиеБуквы(Строка)

	Алфавит = "абвгдеёжзийклмнопрстуфхцчшщъыьэюя"; // BSLLS:Typo-off
	Для Порядок = 1 По СтрДлина(Алфавит) Цикл
		Если СтрНайти(Строка, Сред(Алфавит, Порядок, 1)) > 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;

КонецФункции